// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Decode.
    Not(in=instruction[15], out=isAins);

    // Get current A register.
    Mux16(a=prevAin, b=instruction, sel=isAins, out=aIns);
    Or(a=isAins, b=prevWriteA, out=aRegLoad);
    ARegister(in=aIns, load=aRegLoad, out=aRegOut, out[0..14]=addressM);
    
    // Decide ALU input from A and M.
    Mux16(a=aRegOut, b=inM, sel=instruction[12], out=aOrMAluIn);

    // ALU input D.
    DRegister(in=prevDin, load=prevWriteD, out=dAluIn);

    // ALU op.
    ALU(x=dAluIn, y=aOrMAluIn, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, zr=aluZr, ng=aluNg);

    // Destinations.
    Not16(in=inM, out=notInM);
    And16(a=inM, b=notInM, out=zero16);
    And(a=instruction[5], b=instruction[15], out=prevWriteA);
    And(a=instruction[4], b=instruction[15], out=prevWriteD);
    And(a=instruction[3], b=instruction[15], out=prevWriteM, out=writeM);
    Mux16(a=zero16, b=aluOut, sel=prevWriteA, out=prevAin);
    Mux16(a=zero16, b=aluOut, sel=prevWriteD, out=prevDin);
    Mux16(a=zero16, b=aluOut, sel=prevWriteM, out=outM);

    // Jumps.
    Not(in=aluZr, out=notZr);
    Not(in=aluNg, out=notNg);
    And(a=notZr, b=notNg, out=pos);

    And(a=instruction[2], b=aluNg, out=islt);
    And(a=instruction[1], b=aluZr, out=iseq);
    And(a=instruction[0], b=pos, out=isgt);
    Or(a=islt, b=iseq, out=islteq);
    Or(a=islteq, b=isgt, out=islteqgt);
    And(a=islteqgt, b=instruction[15], out=isJump);

    Not(in=isJump, out=isInc);
    PC(in=aRegOut, load=isJump, inc=isInc, reset=reset, out[0..14]=pc);
}